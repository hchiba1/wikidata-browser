#!/usr/bin/env spang2
# @endpoint wikidata
# @option --time

SELECT DISTINCT ?s (str(?label) AS ?l) (str(?gl) AS ?g) (str(?cl) AS ?c) ?b (year(?death) AS ?d) ?des
WHERE {
  ?s rdfs:label ?label .
  FILTER (lang(?label) = 'en')
  OPTIONAL {
    ?s wdt:P21 ?gender .
    ?gender rdfs:label ?gl .
    FILTER (lang(?gl) = 'en')
  }
  OPTIONAL {
    ?s wdt:P27 ?country .
    ?country rdfs:label ?cl .
    FILTER (lang(?cl) = 'en')
  }
  OPTIONAL {
    ?s wdt:P569 ?birth .
  }
  OPTIONAL {
    ?s wdt:P570 ?death .
  }
  OPTIONAL {
    ?s schema:description ?des .
    FILTER (lang(?des) = 'en')
  }
  BIND(year(?birth) AS ?b)
  VALUES (?s) { {{INPUT}} }
}
